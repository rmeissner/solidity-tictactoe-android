apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'bivrost'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "de.thegerman.sttt"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        project.ext.set("archivesBaseName", String.format("solidity-tictactoe-%s", defaultConfig.versionCode))

        buildConfigField javaTypes.STRING, "GAME_ADDRESS", asString(getKey("GAME_ADDRESS", "0x8335552d5bee1882c4779e205fc11453facf0c9a")) // unoptimized: 0xbc8d600F956D39F9533b3526E3c7E3e4f117f14d
        buildConfigField javaTypes.STRING, "BLOCKCHAIN_NET_URL", asString(getKey("BLOCKCHAIN_NET_URL", "https://kovan.infura.io/"))
        buildConfigField javaTypes.STRING, "INFURA_API_KEY", asString(getKey("INFURA_API_KEY", ""))
        buildConfigField javaTypes.STRING, "ETHERSCAN_TX_URL", asString(getKey("ETHERSCAN_TX_URL", "https://kovan.etherscan.io/tx/"))
        buildConfigField javaTypes.LONG, "GAS_PRICE_OVERRIDE", getKey("GAS_PRICE_OVERRIDE", "4_000_000_000L")

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
                proguardFile 'proguard-rules.pro'
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Android
    implementation "com.android.support:appcompat-v7:$versions.android_support"
    implementation "com.android.support:cardview-v7:$versions.android_support"
    implementation "com.android.support:design:$versions.android_support"

    //Rx
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxjava"
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rxkotlin"
    implementation "com.gojuno.koptional:koptional:$versions.koptional"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"
    implementation("com.jakewharton.rxbinding2:rxbinding-kotlin:$versions.rxbinding") {
        exclude group: "io.reactivex.rxjava2", module: "rxandroid"
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$versions.rxbinding") {
        exclude group: "io.reactivex.rxjava2", module: "rxandroid"
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$versions.rxbinding") {
        exclude group: "io.reactivex.rxjava2", module: "rxandroid"
    }

    //Android Architecture Components
    implementation "android.arch.lifecycle:extensions:$versions.architecture_components"
    kapt "android.arch.lifecycle:compiler:$versions.architecture_components"
    implementation "android.arch.persistence.room:runtime:$versions.architecture_components"
    kapt "android.arch.persistence.room:compiler:$versions.architecture_components"
    implementation "android.arch.persistence.room:rxjava2:$versions.architecture_components"
    implementation "com.android.support:multidex:$versions.multidex"

    //Dagger
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"

    // Bivrost
    implementation "com.github.gnosis.bivrost-kotlin:bivrost-solidity-types:$versions.bivrost"

    // Timber
    implementation "com.jakewharton.timber:timber:$versions.timber"

    // Accounts
    implementation 'com.github.gnosis.heimdall-android:kethereum:master-SNAPSHOT'

    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "org.mockito:mockito-inline:$versions.mockito"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$versions.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
